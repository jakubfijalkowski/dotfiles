pkgbase='dotnet2.2'
pkgname=('dotnet2.2-runtime-bin' 'aspnet2.2-runtime-bin' 'dotnet2.2-sdk-bin')
_runtimever=2.2.7
_sdkver=2.2.402
pkgver=${_runtimever/-/}
pkgver=${pkgver//-/+}
pkgrel=1
arch=('x86_64')
url='https://www.microsoft.com/net/core'
license=('MIT')
options=(staticlibs)
source=('https://download.visualstudio.microsoft.com/download/pr/46411df1-f625-45c8-b5e7-08ab736d3daa/0fbc446088b471b0a483f42eb3cbf7a2/dotnet-sdk-2.2.402-linux-x64.tar.gz')
sha256sums=('SKIP')

package_dotnet2.2-runtime-bin()
{
    pkgdesc='The .NET Core 2.2 runtime'
    depends=("dotnet-host>=${pkgver}" 'icu' 'krb5' 'libunwind' 'openssl-1.0' 'zlib' 'libcurl.so')
    optdepends=('lttng-ust: CoreCLR tracing')
    provides=("dotnet-runtime=${pkgver}")

    install -d "${pkgdir}"/usr/share/{dotnet/shared,licenses}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/shared/Microsoft.NETCore.* \
        "${pkgdir}/usr/share/dotnet/shared/"

    ln -s dotnet-host "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_aspnet2.2-runtime-bin()
{
    pkgdesc='The ASP.NET Core 2.2 runtime'
    depends=("dotnet-runtime=${pkgver}")
    provides=("aspnet-runtime=${pkgver}")

    install -d "${pkgdir}"/usr/share/{dotnet/shared,licenses}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/shared/Microsoft.AspNetCore.* \
        "${pkgdir}/usr/share/dotnet/shared/"

    ln -s dotnet-host "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_dotnet2.2-sdk-bin()
{
    pkgdesc='The .NET Core 2.2 SDK'
    pkgver=${_runtimever/-/}
    pkgver=${pkgver//-/+}
    depends=("dotnet-runtime=${pkgver}")
    pkgver=${_sdkver/-/}
    pkgver=${pkgver//-/+}
    provides=("dotnet-sdk=${pkgver}")

    install -d "${pkgdir}"/usr/share/{dotnet,licenses}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/sdk \
        "${pkgdir}/usr/share/dotnet/"

    ln -s dotnet-host "${pkgdir}/usr/share/licenses/${pkgname}"
}
