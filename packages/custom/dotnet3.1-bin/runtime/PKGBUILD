pkgbase='dotnet3.1-bin'
pkgname=('dotnet3.1-host-bin' 'dotnet3.1-runtime-bin')
_runtimever=3.1.1
_aspnetver=3.1.1
_sdkver=3.1.101
pkgver=${_runtimever/-/}
pkgver=${pkgver//-/+}
pkgrel=1
arch=('x86_64')
url='https://www.microsoft.com/net/core'
license=('MIT')
options=(staticlibs)
source=('https://download.visualstudio.microsoft.com/download/pr/c4b503d6-2f41-4908-b634-270a0a1dcfca/c5a20e42868a48a2cd1ae27cf038044c/dotnet-sdk-3.1.101-linux-x64.tar.gz')
sha512sums=('eeee75323be762c329176d5856ec2ecfd16f06607965614df006730ed648a5b5d12ac7fd1942fe37cfc97e3013e796ef278e7c7bc4f32b8680585c4884a8a6a1')

package_dotnet3.1-host-bin()
{
    pkgdesc='A generic driver for the .NET Core Command Line Interface'
    depends=('gcc-libs')
    provides=("libhostfxr=${pkgver}" "dotnet-host=${pkgver}" "dotnet-hostfxr=${pkgver}")
    conflicts=('dotnet-host' 'dotnet-hostfxr' 'dotnet-host-preview' 'libhostfxr')
    replaces=('dotnet-host' 'dotnet-hostfxr' 'dotnet-host-preview' 'libhostfxr')

    install -d "${pkgdir}"/usr/{bin,lib,share/dotnet,share/licenses/dotnet-host}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/{host,dotnet,LICENSE.txt,ThirdPartyNotices.txt} \
        "${pkgdir}/usr/share/dotnet/"

    ln -s /usr/share/dotnet/dotnet "${pkgdir}/usr/bin/dotnet"
    ln -s /usr/share/dotnet/host/fxr/"${_runtimever}"/libhostfxr.so "${pkgdir}/usr/lib/libhostfxr.so"
    ln -s /usr/share/dotnet/LICENSE.txt "${pkgdir}/usr/share/licenses/dotnet-host/LICENSE.txt"
    ln -s /usr/share/dotnet/ThirdPartyNotices.txt "${pkgdir}/usr/share/licenses/dotnet-host/ThirdPartyNotices.txt"
}

package_dotnet3.1-runtime-bin()
{
    pkgdesc='The .NET Core 3.1 runtime'
    depends=("dotnet-host>=${pkgver}" 'icu' 'krb5' 'libunwind' 'openssl' 'zlib' 'libcurl.so')
    optdepends=('lttng-ust: CoreCLR tracing')
    provides=("dotnet-runtime=${pkgver}")

    install -d "${pkgdir}"/usr/share/{dotnet/shared,licenses}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/shared/Microsoft.NETCore.* \
        "${pkgdir}/usr/share/dotnet/shared/"

    ln -s dotnet-host "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_aspnet3.1-runtime-bin()
{
    pkgdesc='The ASP.NET Core 3.1 runtime'
    _runtimever=${_runtimever/-/}
    _runtimever=${_runtimever//-/+}
    depends=("dotnet-runtime=${_runtimever}")

    install -d "${pkgdir}"/usr/share/{dotnet/shared,licenses}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/shared/Microsoft.AspNetCore.* \
        "${pkgdir}/usr/share/dotnet/shared/"

    ln -s dotnet-host "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_dotnet3.1-sdk-bin()
{
    pkgdesc='The .NET Core 3.1 SDK'
    _runtimever=${_runtimever/-/}
    _runtimever=${_runtimever//-/+}
    depends=("dotnet-runtime=${_runtimever}")

    install -d "${pkgdir}"/usr/share/{dotnet,licenses}

    cp -dr --no-preserve=ownership \
        "${srcdir}"/{packs,sdk,templates} \
        "${pkgdir}/usr/share/dotnet/"

    ln -s dotnet-host "${pkgdir}/usr/share/licenses/${pkgname}"
}
