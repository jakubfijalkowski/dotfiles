pkgname=('dotnet2-runtime-bin' 'aspnet2-runtime-bin' 'dotnet2-sdk-bin')
pkgver=2.2.6+401
pkgrel=1
arch=('x86_64')
url='https://www.microsoft.com/net/core'
license=('MIT')
options=(staticlibs)
source=('https://download.visualstudio.microsoft.com/download/pr/228832ea-805f-45ab-8c88-fa36165701b9/16ce29a06031eeb09058dee94d6f5330/dotnet-sdk-2.2.401-linux-x64.tar.gz')
sha256sums=('SKIP')

package_dotnet2-runtime-bin()
{
    pkgdesc='The .NET Core runtime'
    depends=("dotnet-host>=${pkgver}" 'icu' 'krb5' 'libunwind' 'openssl-1.0' 'zlib' 'libcurl.so')
    optdepends=('lttng-ust: CoreCLR tracing')
    provides=("dotnet-runtime=${pkgver}")

    cd "${srcdir}"
    install -d "${pkgdir}"/usr/share/{dotnet/shared,licenses}
    cp -dr --no-preserve=ownership shared/Microsoft.NETCore.App "${pkgdir}"/usr/share/dotnet/shared/
    ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet2-runtime
}

package_aspnet2-runtime-bin()
{
    pkgdesc='The ASP.NET Core runtime'
    depends=("dotnet-runtime=${pkgver}")
    provides=("aspnet-runtime=${pkgver}")

    cd "${srcdir}"
    install -d "${pkgdir}"/usr/share/{dotnet/shared,licenses}
    cp -dr --no-preserve=ownership shared/Microsoft.AspNetCore.App "${pkgdir}"/usr/share/dotnet/shared/
    ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet2-runtime
}

package_dotnet2-sdk-bin()
{
    pkgdesc='The .NET Core SDK'
    depends=("dotnet-runtime=${pkgver}")
    provides=("dotnet-sdk=${pkgver}")

    cd "${srcdir}"
    install -d "${pkgdir}"/usr/share/{dotnet,licenses}
    cp -dr --no-preserve=ownership sdk "${pkgdir}"/usr/share/dotnet/
    ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet2-sdk
}
