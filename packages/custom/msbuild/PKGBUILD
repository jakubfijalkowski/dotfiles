pkgname=msbuild
pkgver=16.4+xamarinxplat.2019.09.09.15.03
pkgrel=10
pkgdesc='Build system for .NET and Visual Studio'
arch=('any')
url='https://github.com/mono/msbuild'
license=('MIT')
depends=('mono>=6.4.0')
optdepends=('dotnet-host: .NET Core support' 'dotnet-sdk: .NET Core support')
makedepends=('dotnet-host' 'unzip')
provides=("msbuild-sdkresolver=${pkgver}")
conflicts=('msbuild-sdkresolver')
source=("https://download.mono-project.com/sources/msbuild/msbuild-${pkgver}.tar.xz"
        'case_sensitive_dotnetbits.patch'
        'install_path_and_pdb.patch')
sha256sums=('84c2e6021c43ba9dacc530d3d1bd6ed38e4f6b088a5e014f0ac4225843895d28'
            'aaa827b13abfb150572915d3daaa6b7140a7b4c12cda48aa76fed0f830a8bee1'
            '6b89d6f77adba0d5bcebfc3ab4da35b3989c4d862d54818d922dd54c2f022dde')

prepare() {
    cd "${srcdir}/msbuild-${pkgver%+*}"
    ln -f LICENSE license
    patch --forward --strip=1 --input="${srcdir}/case_sensitive_dotnetbits.patch"
    patch --forward --strip=1 --input="${srcdir}/install_path_and_pdb.patch"
    rm -f mono/SdkResolvers/Microsoft.DotNet.MSBuildSdkResolver/libhostfxr.dylib
}

build() {
    cd "${srcdir}/msbuild-${pkgver%+*}"

    ./eng/cibuild_bootstrapped_msbuild.sh \
        --host_type mono \
        --configuration Release \
        --skip_tests \
        /p:DebugSymbols=false \
        /p:DebugType=none
}

package() {
    cd "${srcdir}/msbuild-${pkgver%+*}"

    ./stage1/mono-msbuild/msbuild \
        /p:MonoInstallPrefix="${pkgdir}/usr" \
        /p:Configuration=Release-MONO \
        /p:TargetMSBuildToolsVersion=Current \
        mono/build/install.proj

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
